@echo off

rem **********************************************************************
rem General CMake (out-of-source) makefile generator for Windows platforms
rem **********************************************************************

rem Check that the maximum number of parameters has not been exceeded
if not "%4"=="" goto Usage

rem Directory where the main CMakeLists.txt file resides (converted to absolute path)
set CMAKELISTS_DIR=%~f2

rem Check that it contains a CMakeLists.txt file
if not exist "%CMAKELISTS_DIR%\CMakeLists.txt" goto NotExist

rem Directory where the output files must be saved (converted to absolute path)
set OUTPUT_DIR=%~f3

rem Set up the CMake generator according to the environment
set ENVIRONMENT_NAME=%1
if "%ENVIRONMENT_NAME%"=="MSVC9x86" goto MSVC9x86
if "%ENVIRONMENT_NAME%"=="MSVC9x64" goto MSVC9x64
if "%ENVIRONMENT_NAME%"=="MSVC10x86" goto MSVC10x86
if "%ENVIRONMENT_NAME%"=="MSVC10x64" goto MSVC10x64
if "%ENVIRONMENT_NAME%"=="MinGW" goto MinGW

rem Not a valid environment
goto Usage

:MSVC9x86
set GENERATOR_NAME=Visual Studio 9 2008
goto GenerateMSVC

:MSVC9x64
set GENERATOR_NAME=Visual Studio 9 2008 Win64
goto GenerateMSVC

:MSVC10x86
set GENERATOR_NAME=Visual Studio 10
goto GenerateMSVC

:MSVC10x64
set GENERATOR_NAME=Visual Studio 10 Win64
goto GenerateMSVC

:MinGW
set GENERATOR_NAME=MinGW Makefiles
goto GenerateOther

:GenerateMSVC
rem Generate solutions for Visual Studio: Subdirectories are created by Visual Studio for
rem each configuration, therefore we do not need to generate them here. A single solution
rem containing all configurations is generated by CMake
echo.
echo.Generating %ENVIRONMENT_NAME% makefiles
echo.------------------------------------------------
mkdir "%OUTPUT_DIR%\%ENVIRONMENT_NAME%"
cd "%OUTPUT_DIR%\%ENVIRONMENT_NAME%"
cmake -G "%GENERATOR_NAME%" "%CMAKELISTS_DIR%"

goto End

:GenerateOther
rem Generate solutions for environments different from Visual Studio. A subdirectory must
rem be created for each configuration. In each one CMake will generate the makefiles for
rem the corresponding configuration
echo.
echo.Generating %ENVIRONMENT_NAME% makefiles
echo.------------------------------------------------

echo.Generating Debug makefiles
mkdir "%OUTPUT_DIR%\%ENVIRONMENT_NAME%\Debug"
cd "%OUTPUT_DIR%\%ENVIRONMENT_NAME%\Debug"
cmake -G "%GENERATOR_NAME%" -DCMAKE_BUILD_TYPE=Debug "%CMAKELISTS_DIR%"

echo.
echo.Generating Release makefiles
mkdir "%OUTPUT_DIR%\%ENVIRONMENT_NAME%\Release"
cd "%OUTPUT_DIR%\%ENVIRONMENT_NAME%\Release"
cmake -G "%GENERATOR_NAME%" -DCMAKE_BUILD_TYPE=Release "%CMAKELISTS_DIR%"

echo.
echo.Generating RelWithDebInfo makefiles
mkdir "%OUTPUT_DIR%\%ENVIRONMENT_NAME%\RelWithDebInfo"
cd "%OUTPUT_DIR%\%ENVIRONMENT_NAME%\RelWithDebInfo"
cmake -G "%GENERATOR_NAME%" -DCMAKE_BUILD_TYPE=RelWithDebInfo "%CMAKELISTS_DIR%"

echo.
echo.Generating MinSizeRel makefiles
mkdir "%OUTPUT_DIR%\%ENVIRONMENT_NAME%\MinSizeRel"
cd "%OUTPUT_DIR%\%ENVIRONMENT_NAME%\MinSizeRel"
cmake -G "%GENERATOR_NAME%" -DCMAKE_BUILD_TYPE=MinSizeRel "%CMAKELISTS_DIR%"

goto End

:Usage
echo.
echo.Generates a directory containing all makefiles for a given enviroment
echo.
echo.Usage: %0 ENVIRONMENT CMAKELISTS_DIRECTORY OUTPUT_DIRECTORY
echo.
echo.The supported environments are currently: MSVC9x86, MSVC9x64,
echo.MSVC10x86, MSVC10x64, MinGW
echo.
goto End

:NotExist
echo.Error: No CMakeLists.txt file found in %CMAKELISTS_DIR%
goto End

:End
rem Back to the execution directory
cd "%OUTPUT_DIR%"

rem Cleanup variables
set OUTPUT_DIR=
set ENVIRONMENT_NAME=
set GENERATOR_NAME=
set CMAKELISTS_DIR=